group = 'com.bro2.gradle'
version = '0.2.2'

ext {
    artifactId = 'cmdbatch'
    artifactVersion = version
    vcsTag = 'v0.2.2'
    versionDesc = 'keep path as a sepearate environment'
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile 'junit:junit:4.12'
    testRuntime files(createClasspathManifest)
    testCompile gradleTestKit()
}

repositories {
    jcenter()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId project.group
            artifactId project.artifactId
            version project.artifactVersion
        }
    }
}

bintray {
    user = property('user')
    key = property('key')
    publications = ['MyPublication']
    publish = true
//    override = true

    pkg {
        repo = 'repo-maven-gradle'
        name = project.artifactId
        licenses = ['MIT']
        desc = 'a gradle plugin which can auto execute a batch of commands'
        vcsUrl = 'https://github.com/liu-bro2/gradle-cmdbatch'
        websiteUrl = 'https://github.com/liu-bro2/gradle-cmdbatch'
        issueTrackerUrl = 'https://github.com/liu-bro2/gradle-cmdbatch/issues'

        version {
            name = project.artifactVersion
            desc = project.versionDesc
            released  = new Date()
            vcsTag = project.vcsTag
        }
    }

}